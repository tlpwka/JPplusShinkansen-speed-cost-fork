switch (FEAT_TRAINS, SELF, sw_0_series_graphics, position_in_consist % 3) {				
		1:  sw_0_series_ends; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_0_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_0;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_0_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_0_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

/* SPEED */
    switch (FEAT_TRAINS, SELF, sw_0_series_speed, current_year) {
	    0..1986: return 210;                        // Until 1986, Speed = 210 km/h
	    return 220;                                 // After 1986, Speed = 220 km/h
    }

item(FEAT_TRAINS, series_0) {
	property {
		/* Menu */
        name: 							string(STR_NAME_0_SERIES);
        sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
        misc_flags:                     bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_2CC);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
        /* Availability */
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(1964,10,01);           // Date of launch
		model_life:                     1987 - 1964;		        // Constructed until 1986, in service to 2008
		vehicle_life:                   30;							// Same as JapanSet
        track_type:                     SHINKANSEN;
		/* Cargo */
        reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
		cargo_capacity:                 1;							// 
		loading_speed:                  50;                         // Changed by callback						
		cargo_age_period:               278; 						// Default 185 x 1.5
		/* Stats */
        speed:                          211 km/h;                   // Changed by callback
        power:                          8050 kW;	                // Changed by callback				
		weight:                         1464 ton;                   // Changed by callback					
		tractive_effort_coefficient:    0.3;                        // Standard
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
        additional_text:                string(STR_DESC_0_SERIES);
        cargo_subtype_text:             sw_0_series_year_dep_subtype_text;
		can_attach_wagon:				sw_0_series_attach_vehid;
		start_stop:						sw_0_series_stop_start;
        purchase_weight:                61;
        purchase_power:                 248 * 4;
        /* Graphics */
        default:                        sw_0_series_graphics;
		purchase:                       sw_0_series_year_dep_purchase;
		articulated_part:               sw_0_series_articulated_part;
        visual_effect_and_powered:      sw_visual_effect_and_powered;
		/* Stats */
        length:                        	sw_param_length;
        speed:							sw_0_series_speed;
		weight:                         NumberCarsInConsist() * 61;
        power:                          NumberCarsInConsist() * 248 * 4;	// 185 kW
        cargo_capacity:                 28 * CapacityFactor();															
		loading_speed:					28 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
        cost_factor:                    125 * param_purchase_cost;
		running_cost_factor:            (300 + 125 * NumberCarsInConsist() ) * RunningCostFactor() * param_running_cost;
		purchase_running_cost_factor:   300 * param_running_cost + 125 * param_running_cost * NumberCarsInConsist();
	}
	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_0_series_graphics;
        cargo_capacity:                 28 * CapacityFactor();
        cargo_age_period:				param_decay_express;
        loading_speed:					28 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_param_length;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_0_series_running_costs;
      }
}
